// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.4.20"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

def loadedModules = [:]
getSubprojects().forEach {
    loadedModules.put(it.name, it.path)
}
def group = 'np.com.susanthapa.efficientmultimodule'


allprojects {
    repositories {
        mavenLocal()
        google()
        jcenter()

        // replace configurations for all modules
        configurations.all {
            // get our own dependencies
            resolutionStrategy.dependencySubstitution.all { dependency ->
                if (dependency.requested instanceof ModuleComponentSelector && dependency.requested.group == group && loadedModules.containsKey(dependency.requested.module)) {
                    def targetProject = findProject(loadedModules.get(dependency.requested.module))
                    if (targetProject != null) {
                        dependency.useTarget targetProject
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}